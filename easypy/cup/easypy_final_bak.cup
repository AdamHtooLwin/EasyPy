/* ----------------------User code--------------------*/

package src;
import java.util.*;

parser code {:
    public void sem_error(String context , String message)
    {
        System.err.println("Semantic Error: "+ "<"+context+">" + message);
        System.exit(1);
    }

    public void type_error (String context, String message)
    {
        System.err.println("Type Error:"+ "<"+context+">" + message);
        System.exit(1);
    }
:};
   
/* ------------Grammar symbol declarations----------- */
   
terminal    COMMA, SEMI, EQ, PUBLISH;
terminal    PLUS, MINUS, TIMES, DIVIDE, MOD, LPAR, RPAR, LCURL, RCURL;
terminal    WHILE, DO, IF, ELSE, LT, GT, LTE, GTE, EQUAL, NOTEQUAL, OR, AND;
terminal    INT, BOOL, CHAR, FLOAT;

terminal Integer   INTEGER_LITERAL;
terminal Boolean   BOOLEAN_LITERAL;
terminal Float     FLOATING_LITERAL;
terminal String    CHAR_LITERAL;
terminal String    ID;

non terminal                    program, declaration, declaration_list;
non terminal Type               type;

non terminal Statement              statement, assignment, ifthen, publish, while, until_st;
non terminal StatementList              statement_list;
non terminal Expression               E, T, F, L;
non terminal ArrayList<String>  id_list;

/* Precedences */
precedence left SEMI;
precedence left PLUS, MINUS;
precedence left TIMES;
   
/* ------------Grammar rules----------- */

program ::= declaration_list:d SEMI statement_list:sl
                {: sl.execute(); :};

declaration_list ::= declaration_list:dl SEMI declaration:d | declaration:d;


declaration ::= type:t id_list:il
                {:
                for (String i : il){
//                    System.out.println("Declaring variable " + i + " of type " + t.getCode());
                    Memory m = Memory.getInstance();
                    if (m.SymbolTable.containsKey(i)){
                        parser.sem_error(i,"Duplicate declaration "+i);
                    }
                    m.SymbolTable.put(i, t);
                }
                :};

id_list ::=   id_list:il COMMA ID:i
            {:
                il.add(i);
                RESULT = il;
            :}
          |
            ID:i
            {:
                ArrayList<String> list = new ArrayList<String>();
                list.add(i);
                RESULT = list;
            :};

type ::= INT
            {: RESULT = new Type(Type.INTEGER, "int"); :}
        | FLOAT
            {: RESULT = new Type(Type.FLOAT, "float"); :}
        | CHAR
            {: RESULT = new Type(Type.STRING, "char"); :}
        | BOOL
            {: RESULT = new Type(Type.BOOLEAN, "bool"); :};

statement_list ::= statement_list:sl statement:s
                {: RESULT = new StatementList(sl, s); :}
              | statement:s {: RESULT = new StatementList(s); :};

statement ::= assignment:as SEMI
                {: RESULT = as; :}
                | publish:p SEMI
                {: RESULT = p; :}
                | ifthen:ift
                {: RESULT = ift; :}
                | while:w
                {: RESULT = w; :}
                ;

while ::= WHILE LPAR L:l RPAR LCURL statement:s RCURL
                {:
                if((Boolean) l.getType().isBool()){
                    RESULT = Statement.whileloop(l, s);
                }
                else {
                     parser.type_error("l", " : expression in the while condition must be boolean.");
                }
                :}
                ;

ifthen ::=  IF LPAR L:l RPAR LCURL statement:s RCURL
            {:
                if((Boolean) l.getType().isBool()){
                    RESULT = Statement.ifthen(l, s);
                }
                else {
                     parser.type_error("l", " : expression in the if condition must be boolean.");
                }

            :}
          |
            IF LPAR L:l RPAR LCURL statement:s1 RCURL ELSE LCURL statement:s2 RCURL
            {:
//                System.out.println(l.getType().code);
                if((Boolean) l.getType().isBool()){
                    RESULT = Statement.ifthenelse(l, s1, s2);
                }
                else {
                     parser.type_error("l", " : expression in the if condition must be boolean.");
                }
             :};

assignment ::= ID:i EQ E:e
                {:
                Memory m = Memory.getInstance();
                if (!m.SymbolTable.containsKey(i)){
                    parser.sem_error(i,"Variable undeclared");
                }
                else {
                    if (m.SymbolTable.get(i).tag == e.getType().tag){
                         RESULT = Statement.assignment(i,e);
                    }
                    else {
                        parser.type_error("e", " : expression in the assignment statement must have the type " + m.SymbolTable.get(i).code);
                    }
                }
                :};

publish  ::= PUBLISH LPAR E:e RPAR
                {: RESULT = Statement.print(e); :};

E    ::= E:e PLUS T:t
                {:
                RESULT = new Expression(new Args(e,t), sym.PLUS);
                :}
            | E:e MINUS T:t
                {:
                RESULT = new Expression(new Args(e,t), sym.MINUS);
                :}
            | T:t
                {: RESULT = t; :};

T       ::= T:t TIMES F:f
                {:
                RESULT = new Expression(new Args(t,f), sym.TIMES);
                :}
            | T:t DIVIDE F:f
                {:
                RESULT = new Expression(new Args(t,f), sym.DIVIDE);
                :}
            | F:f
                {:
                RESULT = f;
                :};

F       ::= LPAR E:e RPAR
                {:
                RESULT = e;
                :}
            | INTEGER_LITERAL:n
                {:
                RESULT = new Expression(n);
                :}
            | BOOLEAN_LITERAL:b
                {:
                RESULT = new Expression(b);
                :}
            | FLOATING_LITERAL:f
                {:
                RESULT = new Expression(f);
                :}
            | CHAR_LITERAL:c
                {:
                RESULT = new Expression(c);
                :}
            | ID:i
                {:
                RESULT = new Expression(i, true);
                :}
            ;

L ::= E:e1 AND E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.AND);:}
          |
          E:e1 OR E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.OR);:}
          |
          E:e1 LT E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.LT);:}
          |
          E:e1 GT E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.GT);:}
          |
          E:e1 GTE E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.GTE);:}
          |
          E:e1 LTE E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.LTE);:}
          |
          E:e1 EQUAL E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.EQUAL);:}
          |
          E:e1 NOTEQUAL E:e2
          {:RESULT = new Expression(new Args(e1, e2), sym.NOTEQUAL);:};


